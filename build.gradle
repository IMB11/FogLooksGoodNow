plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

version =  property("mod.version") + "+" + stonecutter.current.version
group = "com.mineblock11"

base {
    archivesName = property("mod.name")
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = "https://mvn.devos.one/snapshots/" }
    maven { url 'https://maven.wispforest.io' }
    maven { url 'https://maven.imb11.dev/releases' }
    maven { url "https://maven.isxander.dev/releases" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://maven.quiltmc.org/repository/release" }
    maven { url 'https://maven.terraformersmc.com/releases' }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.version}"
    mappings "net.fabricmc:yarn:${property('deps.yarn')}:v2"

    modImplementation "net.fabricmc:fabric-loader:${property('deps.fabric_loader')}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${property('deps.fabric_api')}"

    include modImplementation("dev.isxander.yacl:yet-another-config-lib-fabric:${property("deps.yacl_version")}")
    modImplementation "com.mineblock11:MRU:${property("deps.mru_version")}"

    modRuntimeOnly modCompileOnly("com.terraformersmc:modmenu:${property("runtime.modmenu_version")}")

    modCompileOnly "maven.modrinth:sodium:${property("runtime.sodium_version")}"
    modCompileOnly "maven.modrinth:iris:${property("runtime.iris_version")}"
    // Libs required by Iris
    implementation ("io.github.douira:glsl-transformer:2.0.0")
    implementation ("org.antlr:antlr4-runtime:4.11.1")
    implementation ("org.anarres:jcpp:1.4.14")
}

if (stonecutter.current.active) { // run configs for non-active version would be invalid
    loom {
        runConfigs.all {
            ideConfigGenerated = true // generate IDE tasks for running client, server and testmod, datagen if those are present
            runDir "../../run" // use a global run directory for all versions
        }
    }
} else {
    loom {
        runConfigs.all {
            ideConfigGenerated = false // Disable IDE tasks for non-active versions.
        }
    }
}

loom {
    accessWidenerPath = file("../../src/main/resources/flg.accesswidener")
}

processResources {
    filesMatching("fabric.mod.json") {
        expand 'mod_version': project.version,
               'target_minecraft': project.property('mod.target')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}